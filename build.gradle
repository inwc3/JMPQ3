plugins {
    id "idea"
    id "jacoco"
    id 'com.github.kt3k.coveralls' version '2.12.0'
    id "java"
    id 'maven-publish'
}

group 'systems.crigges'
version '1.9.5'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

jacoco {
    toolVersion = "0.8.7"
}

task dist(type: Jar) {
    from files(sourceSets.main.output.classesDirs)
    from files(sourceSets.main.output.resourcesDir)
    from { configurations.runtimeClasspath.collect { zipTree(it) } }
}
dist.dependsOn classes

dist.archiveName = "${jar.baseName}.${jar.extension}"

dependencies {
    implementation 'com.jcraft:jzlib:1.1.3'
    implementation group: 'org.apache.commons', name: 'commons-compress', version: '1.21'
    implementation 'com.github.eustas:CafeUndZopfli:5cdf283e67'
    implementation group: 'org.tukaani', name: 'xz', version: '1.9'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.31'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    testImplementation 'org.testng:testng:7.4.0'
}

test {
    useTestNG()
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

artifacts {
    archives jar
    archives sourcesJar
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'inwc3'
            artifactId = 'jmpq3'

            from components.java
        }
    }
}